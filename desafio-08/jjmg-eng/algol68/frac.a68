#!/usr/bin/a68g --script
##########################################################
# https://jmvdveer.home.xs4all.nl/en.algol-68-genie.html #
#                                                        #
# Instalação em distribuições Linux baseadas em Debian:  #
# $sudo apt-get install algol68g                         #
#                                                        #
# Para executar este programa:                           #
# $chmod +x frac.a68                                     #
# $./frac.a68                                            #
# $./frac.a68 ex1.txt                                    #
#                                                        #
# João J.M. Gonçalves - Janeiro 2021                     #
##########################################################

PROC atoi = (REF STRING s) INT:
    BEGIN
        FILE f; INT i;
        associate(f, s);
        get(f,i);
        i
    END;

PROC mdc = (INT a,b) INT:
    BEGIN
        INT m;
        IF a = 0 THEN m := b
        ELSE          m := mdc(b MOD a, a)
        FI;
        m
    END;

PROC simplify = (REF INT a, b) INT:
    BEGIN
        INT c :=  a % b;
        INT d := a MOD b;
        INT m := mdc(d, b);
        a := d % m;
        b %:= m;
        c
    END;

FILE inf;
STRING input file name;
STRING line;
BOOL finished reading := FALSE;

IF argc < 4 THEN
    print("Nome do arquivo:");
    input file name := read string
ELSE
    input file name := argv(4)
FI;
INT open error := open(inf, input file name, stand in channel);
on logical file end (inf, (REF FILE inf) BOOL: finished reading := TRUE);
IF open error /= 0 THEN print(("Erro na abertura do arquivo. Arquivo inexistente.", newline))
ELSE
    WHILE
        get(inf,(line, new line));
        NOT finished reading
    DO
        STRING s := line;
        INT a := atoi(s);
        INT b := 1;
        INT c;
        INT start, end;
        IF grep in string("/", s, start, end) = 0 THEN
            s := s[end+1 : UPB s];
            b := atoi(s)
        FI;

        IF b = 0 THEN print("ERR") FI;
        IF b = 1 THEN print(whole(a,0)) FI;
        IF b > 1 THEN
            c := simplify(a, b);
            IF c > 0 THEN print((whole(c,0), space)) FI;
            IF a > 0 THEN print((whole(a,0),"/",whole(b,0))) FI
        FI;
        print(newline)
    OD;
    close(inf)
FI

