#!/usr/bin/a68g
##########################################################
# https://jmvdveer.home.xs4all.nl/en.algol-68-genie.html #
#                                                        #
# Instalação em distribuições Linux baseadas em Debian:  #
# $sudo apt-get install algol68g                         #
#                                                        #
# Para executar este programa:                           #
# $a68g palindrome.a68                                   #
#                                                        #
# João J.M. Gonçalves - Janeiro 2021                     #
##########################################################

print("Número incial: ");
INT n1 := read int;
print("Número final: ");
INT n2 := read int;
INT nd1 := 1;
IF n1 /= 0 THEN nd1 := ENTIER(log(n1)+1)
FI;
INT nd2 := ENTIER(log(n2)+1);
INT nd, i;
INT nl := 0;
INT x0 := -1;

PROC result = (INT v, nd, k) VOID:
    BEGIN
        INT start, i, j, x;

        IF v = -1 AND nd > 1 THEN  start := 1
        ELSE                       start := 0
        FI;

        IF nd = 1 THEN  j := 0
        ELSE            j := 1
        FI;

        x := 0;
        IF nd > 0 THEN
            FOR i FROM start TO 9
            DO
                x := (ROUND(10.0 ** (nd - 1)) + j) * i;
                IF v>=0 THEN x := v + x * k
                FI;
                IF (v>0 OR (v = -1 AND nd < 3)) AND x /= x0 AND x >= n1 AND x <= n2 THEN
                    print(x);
                    x0 := x;
                    nl +:= 1;
                    IF nl MOD 10 = 0 THEN print(newline)
                    FI
                FI;
                result(x, nd-2, k * 10)
            OD
        FI
    END;

FOR nd  FROM  nd1 TO  nd2
DO
   result(-1,nd,1)
OD
